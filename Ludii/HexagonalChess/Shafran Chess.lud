// Shafran Chess
// Copyright 2020, Jay M. Coskey, except functions drawn from Chess.lud, as noted
// 2020-07-22: BUG: En passant not working after 3-space initial Pawn move.

//----------------------------------------
// General functions
//----------------------------------------

// Source: Chess.lud - CaptureToPieceAndResetCounter
(define "CaptureToPieceAndResetCounter"
    (apply
        (if (is Enemy (who at:(to)))
            (remove (to)
                (then (set Counter))
            )
        )
    )
)

(define "Directions"
    ("P12" (directions #1) (directions #2))
)

(define "IsFromInStartCell"
    (is In (from) (sites Start (what at:(from))))
)

(define "IsToEmpty"
    (is In (to) (sites Empty))
)

(define "IsToEmptyOrEnemy"
    (or "IsToEmpty"
        (is Enemy (who at:(to)))
    )
)

// Source: Chess.lud - NextCanNotMove
(define "NextCannotMove"
    (not (can Move (do
                (forEach Piece (next))
                ifAfterwards:(not ("IsInCheck" "King" Next))
    )))
)

// Usage: ("P12" <player1_arg> <player2_arg>)
(define "P12"
    (if (is Mover P1) #1 #2)
)

// Usage: ("SlideCaptureMove" <directions> <then>)
(define "SlideCaptureMove"
    (move
        Slide
        #1
        (to if:(is Enemy (who at:(to)))
            "CaptureToPieceAndResetCounter"
        )
        #2
    )
)

//----------------------------------------
// History (for castling)
//----------------------------------------

// Source: Chess.lud - PieceHasNeverMoved
// Usage: ("History_HasNeverMoved" <piece_name_key>)
(define "History_HasNeverMoved"
    (= (state at:(mapEntry #1 (mover))) 1)
)

// Source: Chess.lud - PieceHasMoved
(define "History_SaveMovement"
    (set State (last To) 0)
)

// Source: Chess.lud - RememberPieceHasMoved
(define "History_SaveMovementChange"
    (then
        (if (= (state at:(last To)) 1)
            "History_SaveMovement"
        )
    )
)

//----------------------------------------
// King movement
//----------------------------------------

// Usage: ("KingCaptureStep" <directions> <then>)
(define "KingCaptureStep"
    (move
        Step
        #1
        (to if:"IsToEmptyOrEnemy"
            "CaptureToPieceAndResetCounter"
        )
        #2
    )
)

// Source: Chess.lud - KingNotCheckedAndToEmpty
(define "KingNotCheckedAndToEmpty"
    (and
        "IsToEmpty"
        (not ("IsInCheck" "King" Mover at:(to)))
    )
)

//----------------------------------------
// King movement: Castling
//----------------------------------------

(define "Castle_PreCheck"
    (and
        {
        (= (what at:(mapEntry "King" (mover))) (id "King" Mover))  // At Start Cell
        ("History_HasNeverMoved" "King")
        (not ("IsInCheck" "King" Mover))
        }
    )
)

// Usage: ("Castle_Base" <piece_key> <slide_dir> <slide_spaces> <condition> <then>)
// Source: Chess.lud - DoCastle
(define "Castle_Base"
    (move
        Slide
        (from (mapEntry #1 (mover)))
        #2
        (between
            (exact #3)
            if:#4
        )
        #5
    )
)

// Usage: ("Castle_KingRook" <king_dir> <king_spaces> <rook_side> <rook_dir> <rook_spaces>)
(define "Castle_KingRook"
    ("Castle_Base"
        "King"
        #1
        #2
        "KingNotCheckedAndToEmpty"
        (then (and ("History_SaveMovement") ("Castle_Base" #3 #4 #5 true)))
    )
)

// Usage: ("Castle" <king_dir> <king_spaces> <rook_side> <rook_dir> <rook_spaces>)
// Example: ("Castle" "E" 3 "RookRight" "W" 2)
(define "Castle"
    (if (and
            ("History_HasNeverMoved" #3)
            (can Move ("Castle_Base" #3 #4 #5 "IsToEmpty"))
        )
        ("Castle_KingRook" #1 #2 #3 #4 #5)
    )
)

(define "Castle_BishopSide_Long_P1"  ("Castle" ENE 3 "RookRight" WSW 2))
(define "Castle_BishopSide_Short_P1" ("Castle" ENE 2 "RookRight" WSW 3))
(define "Castle_QueenSide_Long_P1"   ("Castle" WNW 3 "RookLeft"  ESE 2))
(define "Castle_QueenSide_Short_P1"  ("Castle" WNW 2 "RookLeft"  ESE 3))

(define "Castle_BishopSide_Long_P2"  ("Castle" WSW 3 "RookLeft"  ENE 2))
(define "Castle_BishopSide_Short_P2" ("Castle" WSW 2 "RookLeft"  ENE 3))
(define "Castle_QueenSide_Long_P2"   ("Castle" ESE 3 "RookRight" WNW 2))
(define "Castle_QueenSide_Short_P2"  ("Castle" ESE 2 "RookRight" WNW 3))

(define "Castle_BishopSide_Long"
    ("P12" "Castle_BishopSide_Long_P1"  "Castle_BishopSide_Long_P2")
)
(define "Castle_BishopSide_Short"
    ("P12" "Castle_BishopSide_Short_P1" "Castle_BishopSide_Short_P2")
)
(define "Castle_QueenSide_Long"
    ("P12" "Castle_QueenSide_Long_P1"   "Castle_QueenSide_Long_P2")
)
(define "Castle_QueenSide_Short"
    ("P12" "Castle_QueenSide_Short_P1"  "Castle_QueenSide_Short_P2")
)

//----------------------------------------
// Pawns: Single step (non-capturing)
// Note: Counter is reset in (piece "Pawn" ...).
//----------------------------------------

// Source: Derived from Chess.lud - DoubleStep
// Usage: ("PawnHop" <fwd_dir> <back_dir>)
(define "PawnHop"
    (move
        Hop
        #1
        (between if:(is In (between) (sites Empty)))
        (to if:"IsToEmpty")
        (then (and
                {
                (set Pending (ahead (last To) #2))
                (set Var (last To))
                }
        ))
    )
)

(define "PawnStep_Double"
    ("PawnHop" Forward Backward)
)

// Source: Chess.lud - CaptureForwardDiagonal
(define "PawnCapture_Diag"
    (move
        Step
        ("Directions" {NNW NNE} {SSW SSE})
        (to if:(is Enemy (who at:(to)))
            (apply (remove (to)))
        )
    )
)

// Shafran Chess includes an initial Pawn moves with a 3-space jump.
// This allows an e.p. capture to any of the skipped spaces.
// Source: Modified from Double Chess.lud - JumpOfPawn
// 2020-07-22: BUG: En passant not working after 3-space initial Pawn move.
(define "PawnStep_Triple"
    (move
        Hop
        (from)
        Forward
        (between
            (range 2 2)
            if:(is Empty at:(between))
            (apply (set Pending (between)))
        )
        (to if:(is Empty at:(to)))
        (then (set Var (last To)))
    )
)

//----------------------------------------
// Pawn movement: En passant
//   - Save skipped-over spaces in Pending
//   - Save location of last-moved Pawn in Var.
// Note: Counter is reset in (piece "Pawn" ...).
//----------------------------------------

// Usage: ("EnPassant_Base" <directions>)
(define "EnPassant_Base"
    (move
        Step
        #1
        (to if:"IsEnPassantCapture")
        (then (remove (var)))
    )
)

(define "EnPassant_Diag"
    ("EnPassant_Base" ("Directions" {NNW NNE} {SSW SSE}))
)

(define "IsEnPassantCapture"
    (and
        (is Pending)
        (= (to) (value Pending))
    )
)

//----------------------------------------
// Pawn promotion
//----------------------------------------
// Usage: ("PromoteTo" <piece_types>)
(define "PromoteTo"
    (move Promote (last To) #1 (who Mover))
)

//------------------------------------------------------------------------------

(game "Shafran Chess"
    (players {(player N) (player S)})
    (equipment
        {
        (board
            (remove
                (rotate 90 (hex 6))
                cells:{0..5 85..90 84 77 69 60 50 39 29 20 12}
            )
        )

        (piece "King" Each
            (or {
                ("KingCaptureStep" All "History_SaveMovementChange")
                (if "Castle_PreCheck"
                    (or {
                        "Castle_BishopSide_Long"
                        "Castle_BishopSide_Short"
                        "Castle_QueenSide_Long"
                        "Castle_QueenSide_Short"
                        }
                    )
                )
                }
            )
        )
        (piece "Queen"  Each ("SlideCaptureMove" All ~))
        (piece "Rook"   Each ("SlideCaptureMove" Orthogonal "History_SaveMovementChange"))
        (piece "Bishop" Each ("SlideCaptureMove" Diagonal ~))
        (piece "Knight" Each
            (leap
                "KnightWalk"
                (to if:"IsToEmptyOrEnemy"
                    "CaptureToPieceAndResetCounter"
                )
            )
        )

        (piece "Pawn" Each
            (or {
                "StepForwardToEmpty"
                (if "IsFromInStartCell"
                    (or {
                        (if (is In (from) (sites Mover "Pawn_Step2Cells"))
                            "PawnStep_Double"
                        )
                        (if (is In (from) (sites Mover "Pawn_Step3Cells"))
                            "PawnStep_Triple"
                        )
                        }
                    )
                )
                "PawnCapture_Diag"
                "EnPassant_Diag"
                }
                (then
                    (and
                        (if (is In (last To) (sites Mover "PromotionZone"))
                            (moveAgain)
                        )
                        (set Counter)
                    )
                )
            )
        )

        (map "King"      {(pair 1 "E1") (pair 2 "E19")})
        (map "RookLeft"  {(pair 1 "A5") (pair 2 "A15")})
        (map "RookRight" {(pair 1 "I5") (pair 2 "I15")})

        (regions "Pawn_Step2Cells" P1 (sites {"B6"  "C5"  "D4"  "E3"  "F4"  "G5"  "H6"}))
        (regions "Pawn_Step2Cells" P2 (sites {"B14" "C15" "D16" "E17" "F16" "G15" "H14"}))

        (regions "Pawn_Step3Cells" P1 (sites             {"D4"  "E3"  "F4"}))
        (regions "Pawn_Step3Cells" P2 (sites             {"D16" "E17" "F16"}))

        (regions "PromotionZone" P1 (union (sites Side NW) (sites Side NE)))
        (regions "PromotionZone" P2 (union (sites Side SW) (sites Side SE)))

        (regions "Region-Dark"   (sites Phase 2))
        (regions "Region-Light"  (sites Phase 0))
        (regions "Region-Medium" (sites Phase 1))
        }
    )

    (rules
        (start
            {
            (place "King1"   coord:"E1" state:1)
            (place "Queen1"  coord:"D2")
            (place "Rook1"   {"A5" "I5"} state:1)
            (place "Bishop1" {"C3" "F2" "H4"})
            (place "Knight1" {"B4" "G3"})

            (place "King2"   coord:"E19" state:1)
            (place "Queen2"  coord:"F18")
            (place "Rook2"   {"A15" "I15"} state:1)
            (place "Bishop2" {"B16" "D18" "G17"})
            (place "Knight2" {"C17" "H16"})

            (place "Pawn1" {"A7"  "B6"  "C5"  "D4"  "E3"  "F4"  "G5"  "H6"  "I7"})
            (place "Pawn2" {"A13" "B14" "C15" "D16" "E17" "F16" "G15" "H14" "I13"})
            }
        )
        phases:{
        (phase "Movement"
            (play
                (if ("SameTurn")
                    ("PromoteTo" (what {"Queen" "Rook" "Bishop" "Knight"}))
                    (do (forEach Piece)
                        ifAfterwards:(not ("IsInCheck" "King" Mover))
                    )
                )
            )
            (end
                {
                (if (and
                        ("IsInCheck" "King" Next)
                        ("NextCannotMove")
                    )
                    (result Mover Win)
                )
                (if (or
                        (no Moves Mover)
                        (= (counter) 100)
                    )
                    (result Mover Draw)
                )
                }
            )
        )
        }
    )
)

//------------------------------------------------------------------------------

(metadata

    (info
        {
        (version "0.9.4")

        (description "A chess variant played on a board made of hexagons")
        (aliases { "Shafran's Chess" "Hexagonal Chess" })
        (origin "Shafran Chess was invented by Isaak Grigorevich Shafran in 1939, and registered in 1956.")
        (classification "board/war/chess")
        (credit "Jay Coskey, with some small functions drawn from Chess.lud, by Eric Piette")

        (rules "Shafran Chess is played on a 'narrow' hexagonal board that can be thought of as a hexagonal board of size 6 with some outer spaces removed. The board has 70 spaces.

Piece Movement:
  * Queens, Rooks, Bishops, and Knights move as in Glinski Chess.
      - Queens slide in any of the 12 directions.
      - Rooks slide in any of the 6 adjacent directions.
      - Bishops slide in any of the 6 'diagonal' directions.
      - Knights move two spaces in an adjacent direction, then one space in another direction.
  * Kings move as in Glinski Chess, but can also castle. In 'Long Castling', the King moves three spaces toward its queenside Rook and the Rook moves two spaces in the opposite direction. In 'Short Castling', the King moves two spaces toward its bishopside Rook (i.e., the one on the side of the board with two bishops), and the Rook moves three spaces. Castling can only take place when neither the King nor the Rook being moved have moved before.
  * Pawns can advance one space forward without capturing. A Pawn on a Pawn start space can advance any number of spaces, up to the middle row of the board. Thus the outermost Pawns on their first moves can only advance one space, while the Pawns in the three innermost columns can advance up to three spaces. Pawns capture 'diagonally forward' (i.e., to a space ahead connected by an edge, and having the same colour). En passant capture works as in Glinski chess, except that if the opponent just advanced a Pawn three spaces, then the next player can perform en passant capture by landing a Pawn on either of the two spaces that the opponent Pawn just skipped over. On reaching the farthest rank in a given file, Pawns are promoted to a Queen, Rook, Bishop, or Knight, as the player chooses.
")

        (source "For a comparison of popular versions of hexagonal chess, see https://en.wikipedia.org/wiki/Hexagonal_chess. For more details on other chess variants, see The Classified Encyclopedia of Chess Variants, by D. B. Pritchard (2nd edition, completed and edited by John Beasley, 2007).")
        }
    )

    (graphics {
        (show Check)
        (piece Scale "Pawn" 0.825)
        (board Style Chess)

        (region Colour "Region-Dark"   (colour "#b5651d"))
        (region Colour "Region-Light"  (colour "#fff8dc"))
        (region Colour "Region-Medium" (colour "#daae7c"))
    })

    (ai "Shafran Chess_ai")
)
